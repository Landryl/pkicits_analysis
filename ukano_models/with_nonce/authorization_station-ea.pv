(* ==PROTOCOL== *)

let Station(sks:skey, pkaa:pkey, pkea:pkey, enrolmentSKey: skey) =
    (* Make request - Etsi 102941 6.2.3.3.1 *)
    new verificationSKey:skey;
    let verificationPKey = pk(verificationSKey) in

    (** Making the SharedATRequest **)
    new hmacKey:hkey;
    let keytag = hmac(pkey_to_bitstring(verificationPKey), hmacKey) in
    let sharedATRequestHash = hash(keytag) in

    (** Making the ECSignature encrypted for EA **)
    let signedExternalPayload = sign(sharedATRequestHash, enrolmentSKey) in
    new ktmp:key;
    let ECSignature:bitstring = ecies_enc(signedExternalPayload, pkea, ktmp) in
    (* ktmp is actually never used after that, since the EA won't answer to
       the station directly, hence its name *)

    let innerATRequest = (ECSignature, keytag, verificationPKey, hmacKey) in

    (** This model an authorization request message without POP *)
    (** To model with POP, innerATRequest should be signed before encryption **)

    new k:key;
    let m:bitstring = ecies_enc(innerATRequest, pkaa, k) in

    out(c, m);

    in(c, resm:bitstring);
    let res = dec(resm, k) in
    (* Checking station temporary key has been signed by AA *)
    if checksign(res, pkaa) then 0.

(*
let AuthorizationAuthority(skaa:skey, pks:pkey, pkea:pkey) =
    in(c, m:bitstring);
    let (request:bitstring, k:key) = ecies_dec(m, skaa) in
    let (ECSignature:bitstring, keytag:bitstring, verificationPKey:pkey,
         hmacKey:hkey) = request in

    event AAReceivedATRequest;

    if checkhmac(keytag, hmacKey) then

    let tbsData = (ECSignature, keytag) in
    let signedValidationRequest = sign(tbsData, skaa) in
    new kvr:key;
    let validationRequestEncrypted:bitstring =
        ecies_enc(signedValidationRequest, pkea, kvr) in

    event AASentValidationRequest;
    out(c, validationRequestEncrypted);

    in(c, validationResponseEncrypted:bitstring);

    let validationResponse = dec(validationResponseEncrypted, kvr) in
    if checksign(validationResponse, pkea) then
    let requestHash = getmsg(validationResponse) in
    if checkhash(requestHash, validationRequestEncrypted) then

    event AAReceivedValidationResponse;

    let AT = sign(pkey_to_bitstring(verificationPKey), skaa) in

    event AASentATResponse;
    out(c, enc(AT, k));

    0.
*)

let EnrolmentAuthority(skea:skey, enrolmentPKey:pkey, pkaa:pkey) =
    (* RECEIVE AUTHORIZATION VALIDATION REQUEST MESSAGE *)
    in(c, validationRequestEncrypted:bitstring);

    let (validationRequest:bitstring, k:key) =
        ecies_dec(validationRequestEncrypted, skea) in

    if checksign(validationRequest, pkaa) then

    let (ecSignature:bitstring, keytag:bitstring) = getmsg(validationRequest) in

    let (signedExternalPayload:bitstring, ks:key) = ecies_dec(ecSignature, skea) in

    if checksign(signedExternalPayload, enrolmentPKey) then
    let externalPayload = getmsg(signedExternalPayload) in
    if checkhash(externalPayload, keytag) then

    (* The SharedATRequest hash matches the one signed by station, the request
       is genuine. *)

    let requestHash = hash(validationRequestEncrypted) in
    let signedResponse = sign(requestHash, skea) in
    let res = enc(signedResponse, k) in

    (* SEND AUTHORIZATION VALIDATION RESPONSE MESSAGE *)
    out(c, res);
    0.

process
    ! new sks:skey; new skea:skey; new skaa:skey; new enrolmentSKey:skey;
    ! (
         (Station(sks, pk(skaa), pk(skea), enrolmentSKey))
       | (EnrolmentAuthority(skea, pk(enrolmentSKey), pk(skaa)))
    )
