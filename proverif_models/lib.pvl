free c:channel.

(*********************************** ECIES ************************************)

(* The internals of ECIES are not modeled to limit complexity. Abstracted, it
   behaves in a similar way to asymetric encryption. The only difference is that
   a symmetric key is included in the request, that must be used to encrypt the
   response.
*)

type skey.
type pkey.
type key.

fun pk(skey): pkey.

fun ecies_enc(bitstring, pkey, key): bitstring.
reduc forall m: bitstring, sk: skey, k:key; ecies_dec(ecies_enc(m, pk(sk), k), sk) = (m, k).

fun enc(bitstring, key): bitstring.
reduc forall m: bitstring, k: key; dec(enc(m, k), k) = m.

(******************************** SIGNATURES **********************************)

(* Signature are actually dated, so in this version of the equational theory we
   generate a fresh name before signing to simulate that. The downside is that
   we're assuming that dates are never used twice, which might theorically still
   happen, even with dates precised down to the second *)

fun sign_i(bitstring, skey): bitstring.
reduc forall m: bitstring, sk: skey; getmsg_i(sign_i(m, sk)) = m.
reduc forall m: bitstring, sk: skey; checksign(sign_i(m, sk), pk(sk)) = true.

letfun sign(m:bitstring, sk:skey) =
    new date:bitstring;
    sign_i((date, m), sk).

letfun getmsg(m:bitstring) =
    let (date:bitstring, msg:bitstring) = getmsg_i(m) in
    msg.

(********************************** HASHES ************************************)

type hkey.

fun hmac(bitstring, hkey): bitstring.
reduc forall m: bitstring, k: hkey; checkhmac(hmac(m, k), k) = true.

(* Hash used in external payload structures *)
fun hash(bitstring): bitstring.
reduc forall m: bitstring; checkhash(hash(m), m) = true.

(***********************************  MISC  ***********************************)

(* Required to allow encryption of public keys *)
fun pkey_to_bitstring(pkey): bitstring [data, typeConverter].

(******************************************************************************)
